// Base grammar
program = { SOI ~ statements ~ EOI }
identifier = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* ~ !keywords }
keywords = {
	"true" | "false" | "def" | "func" | "do" |
	"end" | "return" | "while" | "break" |
	"continue" | "if" | "then" | "void" |
	"and" | "or" | "not"
}

// Statements
statements = { statement* }
statement = _{
	var_define_stmt |
	var_assign_stmt |
	do_block |
	return_stmt |
	call |
	while_stmt |
	break_stmt |
	continue_stmt |
	if_stmt |
	expr_assign_stmt
}

var_define_stmt = { "def" ~ identifier ~ ("=" ~ expression)? }
var_assign_stmt = { identifier ~ "=" ~ expression }
do_block = { "do" ~ statement* ~ "end" }
return_stmt = { "return" ~ expression? }
while_stmt = { "while" ~ expression ~ do_block }
break_stmt = { "break" }
continue_stmt = { "continue" }
expr_assign_stmt = { expression ~ "=" ~ expression } // This is an unusual one

if_stmt = { "if" ~ expression ~ "then" ~ statements ~ if_elseif* ~ if_else? ~ "end" }
	if_elseif = { ("else" ~ "if" | "elseif") ~ expression ~ "then" ~ statements }
	if_else = { "else" ~ statements }

// Operators
unary_operator = { "-" | "not" }
binary_operator = { "+" | "-" | "/" | "*" | "%" | "==" | "!=" | "and" | "or" | ">=" | "<=" | ">" | "<" | "=>" | "." }

// Expressions
expression = { unary_expression | binary_expression | term }
	unary_expression = { unary_operator ~ term }
	binary_expression = { term ~ (binary_operator ~ term)+ }

term = _{ object | list | call | function | literal | identifier | "(" ~ expression ~ ")" }

object = { "{" ~ (object_entry ~ ("," ~ object_entry)*)? ~ "}" }
	object_entry = { identifier ~ "=" ~ expression }

list = { "[" ~ (expression ~ ("," ~ expression)*)? ~ "]" }

call = { (call_body_empty | call_body_nonempty) ~ "->" ~ expression }
	call_body_empty = { "(" ~ ")" }
	// Note: a rule without parantheses cannot be defined for `expression` call arguments due to left-recursion
	call_body_nonempty = { "(" ~ expression ~ ("," ~ expression)* ~ ")" }

function = { "func" ~ "(" ~ function_def_args? ~ ")" ~ do_block }
	function_def_args = { identifier ~ ("," ~ identifier)* }

// Literals
literal = _{ boolean_literal | string_literal | number_literal }

boolean_literal = { "true" | "false" }
string_literal = { "\"" ~ (!("\"") ~ ANY)* ~ "\"" }
number_literal = @{ ("-"? ~ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* ~ ("." ~ ASCII_DIGIT+)?) | "0" }

// Ignored list
line_comment = _{ "//" ~ (!"\n" ~ ANY)* ~ ("\n" | EOI) }
block_comment = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

COMMENT = _{ line_comment | block_comment }
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }